buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.0-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

minecraft {
	version = "1.8-11.14.4.1563"
	mappings = "snapshot_20141130"
    runDir = "run"
}

repositories {
	maven {
		url "http://mobiusstrip.eu/maven"
	}
	maven { 
		url "http://chickenbones.net/maven/"
	}
}
 
dependencies {
	deobfCompile "mcp.mobius.waila:Waila:1.6.0-B2_1.8"
}

group = project.projectDir.name.toLowerCase()
archivesBaseName = project.projectDir.name + "-mc" + project.minecraft.version

sourceSets.main{
	java {
		srcDirs 'java', 'apis'
	}
	resources {
		srcDirs 'resources'
	}
}

// variable substitution in java files
task processSourceMainJava(type:Copy) {
	inputs.property "vars", project.version + project.minecraft.version
	from('java')
	{
		include '**/ModInfo.java'
		include '**/package-info.java'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	into project.buildDir.getPath()+"/sources/"+SourceSet.MAIN_SOURCE_SET_NAME+"/java"
}

// correct task ordering
afterEvaluate { project ->
	sourceMainJava.finalizedBy(processSourceMainJava)
}

processResources
{
	inputs.property "vars", project.version + project.minecraft.version
	from(sourceSets.main.resources.srcDirs) {
		include '**/*.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude '**/*.info'
	}
}